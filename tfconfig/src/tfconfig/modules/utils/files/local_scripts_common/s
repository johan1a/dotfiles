#!/usr/bin/fish


set default_root "$HOME/dotfiles/tfconfig/src/tfconfig/modules/utils/files/local_scripts"
set -q local_scripts_root || set local_scripts_root $default_root
set dir $local_scripts_root

set index 1

function args-to-file
  echo (string join "/" $argv)
end

function help
  set -l file $dir/(args-to-file $argv)
  if test -f "$file.help"
    cat "$file.help"
  else if test -f $file
    grep help: $file | sed 's/.*help: //' || echo ""
  else
    echo "No help found."
  end
end

if test (count $argv) -eq 0
  echo "Usage: s CMD | s list | s help CMD | s new CMD | s edit CMD"
  exit
end

while test $index -le (count $argv);
  set -l cmd $argv[$index]
  set -l rest_of_args $argv[(math $index + 1)..-1]

  if test "$cmd" = "list";
    list-local-scripts $rest_of_args
    exit
  else if test "$cmd" = "new";
    new-local-script $rest_of_args
    exit
  else if test "$cmd" = "edit";
    edit-local-script $rest_of_args
    exit
  else if test "$cmd" = "help";
    help $rest_of_args
    exit
  else if test -f $dir/$cmd;
    $dir/$cmd $rest_of_args
    break
  else
    if test $index -eq (count $argv);
      echo command not found: $cmd
      exit 1
    end

    set dir $dir/$cmd
    set index (math $index + 1)
  end
end
